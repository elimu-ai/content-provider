plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'ai.elimu.content_provider.utils'
    compileSdk 34

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1002021
        versionName "1.2.21-SNAPSHOT"
        setProperty("archivesBaseName", "utils-${versionName}")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation 'com.github.elimu-ai:model:model-2.0.66' // See https://jitpack.io/#elimu-ai/model
    implementation 'com.github.elimu-ai:analytics:3.1.11@aar' // See https://jitpack.io/#elimu-ai/analytics
}

// See https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
// Usage: ./gradlew clean build publish -PmavenUsername=***** -PmavenPassword=*****
publishing {
    publications {
        utils(MavenPublication) {
            groupId 'ai.elimu.content_provider'
            artifactId 'utils'
            version '1.2.21-SNAPSHOT'
            artifact("${buildDir}/outputs/aar/utils-${version}-release.aar")
        }
    }
    repositories {
        maven {
            credentials(PasswordCredentials)
            url "https://maven.pkg.github.com/elimu-ai/content-provider"
        }
    }
}
