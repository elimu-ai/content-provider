plugins {
    id 'com.android.library'
    id 'maven-publish'
    alias(libs.plugins.kotlin.android)
}

android {
    compileSdk 35
    namespace 'ai.elimu.content_provider.utils'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 1002039
        versionName "1.2.39"
        setProperty("archivesBaseName", "utils-${versionName}")
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation(libs.elimu.model) // See https://jitpack.io/#elimu-ai/model
    implementation(libs.elimu.analytics) // See https://jitpack.io/#elimu-ai/analytics
    implementation libs.androidx.core.ktx
    testImplementation libs.junit
}

// See https://docs.gradle.org/current/dsl/org.gradle.api.publish.maven.MavenPublication.html
// Usage: ./gradlew clean build publish -PmavenUsername=***** -PmavenPassword=*****
publishing {
    publications {
        release(MavenPublication) {
            groupId 'ai.elimu.content_provider'
            artifactId 'utils'
            version '1.2.39'
            artifact("${buildDir}/outputs/aar/utils-${version}-release.aar")
        }
    }
    repositories {
        maven {
            credentials(PasswordCredentials)
            url "https://maven.pkg.github.com/elimu-ai/content-provider"
        }
    }
}

tasks.named("publishReleasePublicationToMavenLocal") {
    dependsOn(":utils:bundleReleaseAar")
}
