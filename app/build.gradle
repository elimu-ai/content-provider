apply plugin: 'com.android.application'
apply plugin: 'org.ajoberstar.grgit'
apply from: "${rootProject.projectDir}/gradle/prepare_release.gradle"
apply from: "${rootProject.projectDir}/gradle/clean_release.gradle"
apply from: "${rootProject.projectDir}/gradle/perform_release.gradle"
apply from: "${rootProject.projectDir}/gradle/ensure_clean.gradle"

android {
    compileSdk 34
    namespace 'ai.elimu.content_provider'

    defaultConfig {
        applicationId "ai.elimu.content_provider"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1002029
        versionName "1.2.29-SNAPSHOT"
        setProperty("archivesBaseName", "${applicationId}-${versionCode}")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "${projectDir}/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        qa_test {
            initWith debug
            applicationIdSuffix ".test"
            versionNameSuffix "-test"
        }
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.github.elimu-ai:model:model-2.0.83' // See https://jitpack.io/#elimu-ai/model

    implementation 'commons-io:commons-io:2.17.0'
    implementation 'com.google.android.material:material:1.5.0'

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment:2.4.2'
    implementation 'androidx.navigation:navigation-ui:2.4.2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'

    // Room components
    implementation "androidx.room:room-runtime:2.4.2"
    annotationProcessor "androidx.room:room-compiler:2.4.2"
    androidTestImplementation "androidx.room:room-testing:2.4.2"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task ensureCleanRepo {
    doLast {
        ensureClean()
    }
}

task releaseClean(dependsOn: ensureCleanRepo) {
    doLast {
        cleanRelease()
    }
}

// Task parameters:
//    bumpVersion -> if available will specify new versionName directly and ignores the `bumpType` parameter.
//    bumpType[major|minor|patch] -> will specify how the version bumping occurs.

task releasePrepare(dependsOn: ensureCleanRepo) {
    doLast {
        prepareRelease()
    }
}

task releasePerform(dependsOn: ensureCleanRepo) {
    doLast {
        performRelease()
    }
}
